apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply from: '../dependencies.gradle'

android {
    
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.airy.v2plus"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            def keyFile = file("../release.keystore")
            keyAlias "release"
            keyPassword "release"
            storeFile keyFile
            storePassword "release"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix '.debug'
            manifestPlaceholders = [
                    APP_NAME: "@string/app_name_debug",
            ]
        }
        release {
            shrinkResources true
            minifyEnabled true
            manifestPlaceholders = [
                    APP_NAME: "@string/app_name",
            ]
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    buildFeatures {
        dataBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "v2_plus_${variant.buildType.name}_${defaultConfig.versionName}_${releaseTime()}.apk"
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"

    // kotlin-coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.coroutinesVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.coroutinesVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$versions.coroutinesVersion"

    // android x and support library
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // android x base component
    implementation 'com.google.android.material:material:1.3.0-alpha01'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.preference:preference-ktx:1.1.1'

    // network
    implementation 'com.squareup.okhttp3:okhttp:4.1.0'
    implementation 'com.squareup.retrofit2:retrofit:2.6.1'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.1.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.6.1'
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'

    // lifecycle
    implementation "androidx.lifecycle:lifecycle-extensions:$versions.lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime:$versions.lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$versions.lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$versions.lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$versions.lifecycleVersion"
    testImplementation "androidx.arch.core:core-testing:$versions.archCompomentVersion"

    // room
    implementation "androidx.room:room-runtime:$versions.roomVersion"
    kapt "androidx.room:room-compiler:$versions.roomVersion"
    implementation "androidx.room:room-ktx:$versions.roomVersion"
    testImplementation "androidx.room:room-testing:$versions.roomVersion"

    // paging
    def paging_version = "2.1.2"
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    testImplementation "androidx.paging:paging-common-ktx:$paging_version"

    // navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$versions.navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$versions.navigationVersion"

    // dagger
    implementation "com.google.dagger:dagger:$versions.daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$versions.daggerVersion"

    // eventbus
    implementation 'org.greenrobot:eventbus:3.1.1'

    // jsoup
    implementation 'org.jsoup:jsoup:1.12.1'

    // glide
    implementation ("com.github.bumptech.glide:glide:$versions.glideVersion") {
        exclude group: "com.android.support"
    }
    implementation ("com.github.bumptech.glide:recyclerview-integration:$versions.glideVersion") {
        // Excludes the support library because it's already included by Glide.
        transitive = false
    }
    implementation "com.github.bumptech.glide:okhttp3-integration:$versions.glideVersion"
    kapt "com.github.bumptech.glide:compiler:$versions.glideVersion"

    // ui component
    implementation 'de.hdodenhof:circleimageview:3.0.1'
    // implementation 'com.facebook.shimmer:shimmer:0.5.0'
    // implementation "com.github.skydoves:rainbow:1.0.1"

    // debugging
    implementation "com.jakewharton.timber:timber:$versions.timberVersion"
}

static def releaseTime() {
    return new Date().format("yyyyMMddhhmmss", TimeZone.getTimeZone("UTC"))
}
